package wayland

import app "../../app"
import "core:bytes"
import "core:fmt"
import "core:mem"
import "core:os"
import "core:sys/linux"
import "egl"
import gl "vendor:OpenGL"


MAX_POOL_SIZE :: app.BUF_WIDTH * app.BUF_HEIGHT * 4 * 4
State :: struct {
	buffer_size, stride, w, h: i32,
	shm_fd:                    linux.Fd,
	shm_pool_data:             []u8,
	status:                    Status,
	wl_registry:               Registry,
	wl_shm:                    Shm,
	wl_shm_pool:               Shm_Pool,
	wl_buffer:                 [2]Buffer,
	xdg_wm_base:               Xdg_Wm_Base,
	xdg_surface:               Xdg_Surface,
	wl_surface:                Surface,
	surface_callback:          Callback,
	tick:                      u32,
	should_redraw:             bool,
	wl_compositor:             Compositor,
	xdg_toplevel:              Xdg_Toplevel,
	wl_seat:                   Seat,
	wl_pointer:                Pointer,
	wl_keyboard:               Keyboard,
	data_device_manager:       Data_Device_Manager,
	data_source:               Data_Source,
	cursor_shape_manager:      Wp_Cursor_Shape_Manager_V1,
	keymap:                    bool,
	use_software:              bool,
	egl_display:               egl.Display,
	egl_surface:               egl.Surface,
	egl_context:               egl.Context,
}
Progress :: enum {
	Continue,
	Exit,
	Crash,
	NoWayland,
}
Status :: enum {
	None,
	SurfaceAckedConfigure,
	SurfaceAttached,
}
GL :: #config(GL, false)

start :: proc() -> Progress {
	conn, display, ok := connect_display()
	if !ok do return .NoWayland
	st := State {
		wl_registry   = display_get_registry(&conn, display),
		w             = 500,
		h             = 500,
		use_software  = !GL,
		should_redraw = true,
	}

	recv_buf: [4096]byte

	st.stride = st.w * 4
	if st.use_software {
		st.buffer_size = st.stride * st.h

		shm_fd, shm_err := create_shm_file(&st.shm_pool_data, MAX_POOL_SIZE)
		if shm_err != .NONE {
			return .Crash
		}
		st.shm_fd = shm_fd
	}
	create_objects(&conn, &st, recv_buf[:])
	defer quit(&conn, st)

	i: u8 = 0
	m: for {
		if err := connection_flush(&conn); err != .NONE {
			fmt.println("FLUSH Error:", err)
			for {
				object, event := peek_event(&conn) or_break
				receive_events(&conn, &st, object, event)
			}
			return .Crash
		}

		{
			result, _ := linux.poll({linux.Poll_Fd{fd = conn.socket, events = {.IN}}}, 2)
			if result > 0 {
				connection_poll(&conn, recv_buf[:])

				for {
					object, event := peek_event(&conn) or_break
					if prog := receive_events(&conn, &st, object, event); prog != .Continue do return prog
				}
				conn.data_cursor = 0
				conn.data = {}
			}
		}
		if st.status == .None do continue

		if st.should_redraw do draw(&conn, &st, u32(i))
		i += 1
	}
	return .Exit
}

draw :: proc(conn: ^Connection, st: ^State, i: u32) {
	using st
	if use_software {
		i := i % len(st.wl_buffer)
		surface_frame(conn, wl_surface)

		app.update_render(
			st.shm_pool_data[i32((i)) * st.buffer_size:][:st.buffer_size],
			st.w,
			st.h,
		)
		surface_attach(conn, wl_surface, wl_buffer[i], 0, 0)
		surface_damage_buffer(conn, wl_surface, 0, 0, w, h)
		surface_commit(conn, wl_surface)
		st.should_redraw = false
	} else {
		gl.ClearColor(1, 0, 0, 1)
		gl.Clear(gl.COLOR_BUFFER_BIT)
		gl.Flush()
		egl.SwapBuffers(egl_display, egl_surface)
	}
}

create_objects :: proc(conn: ^Connection, st: ^State, buff: []byte) -> linux.Errno {
	connection_flush(conn) or_return
	connection_poll(conn, buff)
	for {
		object, event := peek_event(conn) or_break
		if prog := receive_events(conn, st, object, event); prog != .Continue do return .NONE
	}
	conn.data_cursor = 0
	conn.data = {}
	assert(st.wl_compositor != 0)
	assert(st.xdg_wm_base != 0)
	assert(st.wl_seat != 0)

	st.wl_surface = compositor_create_surface(conn, st.wl_compositor)
	st.xdg_surface = xdg_wm_base_get_xdg_surface(conn, st.xdg_wm_base, st.wl_surface)
	st.xdg_toplevel = xdg_surface_get_toplevel(conn, st.xdg_surface)
	st.wl_keyboard = seat_get_keyboard(conn, st.wl_seat)
	st.wl_pointer = seat_get_pointer(conn, st.wl_seat)

	xdg_toplevel_set_title(conn, st.xdg_toplevel, app.TITLE)
	xdg_toplevel_set_app_id(conn, st.xdg_toplevel, app.APP_ID)
	surface_commit(conn, st.wl_surface)

	fmt.println(st.xdg_surface, "xdg_surface")
	fmt.println(st.wl_keyboard, "wl_keyboard")
	fmt.println(st.wl_pointer, "wl_pointer")
	connection_flush(conn) or_return
	connection_poll(conn, buff)
	for {
		object, event := peek_event(conn) or_break
		if prog := receive_events(conn, st, object, event); prog != .Continue do return .NONE
	}
	if st.use_software {
		using st
		if wl_shm != 0 && wl_shm_pool == 0 && wl_buffer == 0 {
			wl_shm_pool = shm_create_pool(conn, wl_shm, auto_cast shm_fd, i32(MAX_POOL_SIZE))
			for &b, i in wl_buffer {
				b = shm_pool_create_buffer(
					conn,
					wl_shm_pool,
					i32(i) * buffer_size,
					w,
					h,
					stride,
					.Argb8888,
				)
			}
			assert(len(shm_pool_data) != 0)
			assert(buffer_size != 0)
			fmt.println(wl_shm_pool, "shm created")
			fmt.println(wl_buffer, "buffers created")
		}
	} else do setup_gl(conn, st)
	return .NONE
}

// https://registry.khronos.org/EGL/sdk/docs/man/html/eglIntro.xhtml
setup_gl :: proc(conn: ^Connection, st: ^State) -> Progress {
	assert(st.wl_surface != 0)
	egl.load_extensions()
	fmt.print("\n\n\n=====================\n")
	major, minor: i32
	egl.BindAPI(egl.OPENGL_API)

	st.egl_display = egl.GetPlatformDisplayEXT(egl.PLATFORM_WAYLAND_KHR, nil, nil)
	// st.egl_display = egl.GetDisplay(egl.DEFAULT_DISPLAY)
	if st.egl_display == nil {
		fmt.println("Failed to create egl display")
		return .Crash
	}
	assert(st.egl_display != {})
	initialized := egl.Initialize(st.egl_display, &major, &minor)
	assert(initialized == egl.TRUE)

	fmt.println(st.egl_display, "egl_display created")
	fmt.printfln("EGL v%i.%i", major, minor)

	config_attribs: [11]i32 = {
		egl.SURFACE_TYPE,
		egl.WINDOW_BIT,
		egl.RENDERABLE_TYPE,
		egl.OPENGL_BIT,
		egl.RED_SIZE,
		8,
		egl.GREEN_SIZE,
		8,
		egl.BLUE_SIZE,
		8,
		egl.NONE,
	}
	num_configs: i32
	configs: [256]egl.Config
	if egl.GetConfigs(st.egl_display, &configs[0], len(configs), &num_configs) {
		fmt.println("configs: ", num_configs)
		fmt.println("configs: ", configs)
	}
	assert(num_configs > 0)
	egl_config: egl.Config
	if egl.ChooseConfig(st.egl_display, &config_attribs[0], &egl_config, 1, &num_configs) {
		fmt.println("configs: ", num_configs)
	}
	assert(num_configs == 1)

	context_attribs := []i32{egl.CONTEXT_MAJOR_VERSION, 2, egl.CONTEXT_MINOR_VERSION, 1, egl.NONE}
	st.egl_context = egl.CreateContext(
		st.egl_display,
		egl_config,
		egl.NO_CONTEXT,
		nil, //&context_attribs[0],
	)
	assert(st.egl_context != {})
	fmt.println(st.egl_context, "egl_context created")

	/* 
	  egl's `NativeWindowType`, required to make a window, is a pointer to the surface type used to create egl_display.
	  In this case a proxy object struct from libwayland's implementation of wayland protocol.
	  To use egl.PLATFORM_WAYLAND_KHR without libwayland (assuming it works) would mean passing in a struct, with
	  function pointers and an event queue, which exactly matched libwayland's as egl will call back
	  into libwayland to run the operations it needs. It's a big dependency injection club, and you're not in it!
	  ============================================================================================================
	  The wl_egl_window which creates said function cannot be "changed to use your " function cannot be modified as
	  To use OpenGl without this requires implementing linux-dmabuf yourself.
	  Info may be found in Mesa's implementation at src/egl/drivers/dri2/platform_wayland.c"
	 * https://ziggit.dev/t/drawing-with-opengl-without-glfw-or-sdl-on-linux/3175/12
	 * https://blaztinn.gitlab.io/post/dmabuf-texture-sharing/
	 */

	// does not work
	wl_egl_surface := shim_wl_surface_proxy(conn, st.wl_surface)
	egl_window, ok := wl_egl_window_create(wl_egl_surface, st.w, st.h)
	assert(ok)
	fmt.println("egl_window: ", egl_window, "\nshim_surface", wl_egl_surface)
	st.egl_surface = egl.CreatePlatformWindowSurfaceEXT(
		st.egl_display,
		egl_config,
		cast(egl.NativeWindowType)egl_window,
		nil,
	)
	fmt.printfln("egl surface: ", st.egl_surface)
	assert(st.egl_surface != {})
	res := egl.MakeCurrent(st.egl_display, st.egl_surface, st.egl_surface, st.egl_context)
	assert(res == true)


	w, h: i32
	egl.QuerySurface(st.egl_display, st.egl_surface, egl.WIDTH, &w)
	egl.QuerySurface(st.egl_display, st.egl_surface, egl.HEIGHT, &h)
	fmt.printfln("egl %ix%i:", w, h)

	fmt.print("\n\n\n=====================")
	return .Continue
}

connect_display :: proc() -> (conn: Connection, display: Display, ok: bool) {
	socket := linux.socket(.UNIX, .STREAM, {.CLOEXEC}, {}) or_else panic("")
	addr: linux.Sock_Addr_Un = {
		sun_family = .UNIX,
	}
	fmt.bprintf(
		addr.sun_path[:],
		"%v/%v",
		os.get_env("XDG_RUNTIME_DIR", context.temp_allocator),
		os.get_env("WAYLAND_DISPLAY", context.temp_allocator),
	)

	if err := linux.connect(socket, &addr); err != .NONE {
		return
	}

	conn, display = display_connect(socket)
	conn.object_types = make([dynamic]Object_Type, 2)
	conn.object_types[1] = .Display

	return conn, display, true
}

create_shm_file :: proc(fb: ^[]u8, size: u32) -> (shm_fd: linux.Fd, err: linux.Errno) {
	shm_fd = linux.Fd(
		linux.syscall(linux.SYS_memfd_create, transmute(^u8)(cstring("wayland-shm")), 1),
	)
	// shm_fd = memfd_create("wayland-shm") or_return
	linux.ftruncate(shm_fd, i64(size)) or_return
	shm_ptr, mmap_err := linux.mmap(0, uint(size), {.READ, .WRITE}, {.SHARED}, shm_fd)
	if mmap_err != .NONE {
		linux.close(shm_fd)
		return 0, mmap_err
	}
	fb^ = mem.slice_ptr(cast(^byte)shm_ptr, int(size))
	return shm_fd, .NONE
}


@(private)
receive_events :: proc(conn: ^Connection, st: ^State, obj: u32, ev: Event) -> Progress {
	#partial switch e in ev {
	case Event_Registry_Global:
		switch e.interface {
		case "wl_shm":
			if st.use_software {
				st.wl_shm = registry_bind(
					conn,
					st.wl_registry,
					e.name,
					e.interface,
					e.version,
					Shm,
				)
			}
		case "xdg_wm_base":
			st.xdg_wm_base = registry_bind(
				conn,
				st.wl_registry,
				e.name,
				e.interface,
				e.version,
				Xdg_Wm_Base,
			)
		case "wl_compositor":
			st.wl_compositor = registry_bind(
				conn,
				st.wl_registry,
				e.name,
				e.interface,
				e.version,
				Compositor,
			)
		case "wl_seat":
			st.wl_seat = registry_bind(conn, st.wl_registry, e.name, e.interface, e.version, Seat)
		}
	case Event_Display_Error:
		fmt.println("[ERROR] code:", e.code, "::", e.object_id, e.message)
	case Event_Shell_Surface_Ping:
		xdg_wm_base_pong(conn, Xdg_Wm_Base(obj), e.serial)
		fmt.println("Received XDG_WM_BASE ping:", e.serial)
	case Event_Xdg_Surface_Configure:
		xdg_surface_ack_configure(conn, st.xdg_surface, e.serial)
		st.status = .SurfaceAckedConfigure
		fmt.println(st.status, e.serial)
	case Event_Shm_Format:
		fmt.println("Received WL_SHM format", e.format)
		if e.format == .Argb8888 {
			fmt.println("ARGB8888 supported by compositor!")
		}
	case Event_Xdg_Toplevel_Configure:
		fmt.println("config: ", e.height, "x", e.height, "|| states: ", e.states, sep = "")
		if e.height == 0 || e.width == 0 do break
		if st.wl_buffer != 0 && st.buffer_size != e.height * e.width * 4 {
			resize_pool(st, e.width, e.height)
			for &b, i in st.wl_buffer {
				if b != 0 do buffer_destroy(conn, b)
				b = shm_pool_create_buffer(
					conn,
					st.wl_shm_pool,
					i32(i) * st.buffer_size,
					st.w,
					st.h,
					st.stride,
					.Argb8888,
				)
			}
			st.status = .None
		}
		if (st.wl_shm_pool != 0 && st.buffer_size * 2 > MAX_POOL_SIZE) {
			shm_pool_resize(conn, st.wl_shm_pool, st.buffer_size)
		}
	case Event_Xdg_Toplevel_Close:
		return .Exit
	case Event_Callback_Done:
		when ODIN_DEBUG do fmt.printfln("%ims", e.callback_data - st.tick)
		st.tick = e.callback_data
		st.should_redraw = true
	case Event_Xdg_Toplevel_Configure_Bounds:
		fmt.println("config bounds: ", e.width, "x", e.height)
	case Event_Xdg_Toplevel_Wm_Capabilities:
		fmt.println("capabilities:", e.capabilities)
	case Event_Seat_Capabilities:
		capabilities := u32(e.capabilities)
		fmt.println("Seat capabilities: ", capabilities)
		pointer_available := capabilities > 1
		keyboard_available := capabilities > 2
		touch_available := capabilities > 4
	case Event_Seat_Name:
	case Event_Keyboard_Keymap:
		fmt.println("Keymap", e.fd, e.size, st.keymap)
		assert(e.format == .Xkb_V1)
		fd: linux.Fd = auto_cast e.fd
		if (!st.keymap) {
			if ptr, err := linux.mmap(0, uint(e.size), {.READ}, {.PRIVATE}, fd); err != .NONE {
				fmt.println("mmap failed, ", err)
				linux.close(fd)
				return .Crash
			} else {
				st.keymap = true
				linux.munmap(ptr, uint(e.size))
			}
			linux.close(fd)
		}
	case:
		when ODIN_DEBUG do fmt.printf("unknown message header: %i; opcode: %i\n", obj, e)
	}
	return .Continue
}

resize_pool :: proc(state: ^State, w, h: i32) -> i32 {
	state.w = w
	state.h = h
	state.stride = state.w * 4
	state.buffer_size = state.stride * state.h
	return state.buffer_size
}

quit :: proc(conn: ^Connection, state: State) {
	for b in state.wl_buffer {
		if b != 0 do buffer_destroy(conn, b)
	}
	if state.wl_shm_pool != 0 do shm_pool_destroy(conn, state.wl_shm_pool)
	if state.xdg_toplevel != 0 do xdg_toplevel_destroy(conn, state.xdg_toplevel)
	if state.xdg_surface != 0 do xdg_surface_destroy(conn, state.xdg_surface)
	if state.wl_surface != 0 do surface_destroy(conn, state.wl_surface)
	if state.wl_seat != 0 do seat_release(conn, state.wl_seat)
	if state.wl_keyboard != 0 do keyboard_release(conn, state.wl_keyboard)
	if state.wl_pointer != 0 do pointer_release(conn, state.wl_pointer)
	connection_flush(conn)
	if len(state.shm_pool_data) > 0 {
		linux.munmap(raw_data(state.shm_pool_data), len(state.shm_pool_data))
	}
	linux.close(state.shm_fd)
	linux.close(conn.socket)
	delete(conn.object_types)
	delete(conn.fds_in)
	delete(conn.fds_out)
	delete(conn.free_ids)
	bytes.buffer_destroy(&conn.buffer)
}

